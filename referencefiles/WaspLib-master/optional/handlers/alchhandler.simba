{$DEFINE WL_ALCH_HANDLER_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

type
  TRSAlchHandler = record(TSRLBaseRecord)
    Item: TRSItem;
    ItemArray: TRSItemArray;
    Timer: TCountDown;
    SlotUnder, LowProfit, HighProfit, TotalProfit: Int32;
    Disabled: Boolean;
    Config: TConfigJSON;
  end;

procedure TRSAlchHandler.SaveConfig();
var
  i: Int32;
  arr: TJSONArray;
begin
  if Self.Config.Has('items') then
  begin
    Self.Config.JSON.getJSONArray('items').Free();
    Self.Config.Remove('items');
  end;

  arr.Init();

  for i := 0 to High(Self.ItemArray) do
    arr.Put(ToStr(Self.ItemArray[i]));
  Self.Config.Put('items', arr);
end;

procedure TRSAlchHandler.AddItem(item: TRSItem);
begin
  Self.ItemArray += item;
  Self.SaveConfig();
end;

procedure TRSAlchHandler.AddDefault();
begin
  Self.ItemArray := [
    'abyssal bracelet(5)', 'adamant platebody', 'air battlestaff',
    'amulet of glory', 'amulet of glory(4)', 'black d''hide body',
    'black d''hide chaps', 'black d''hide shield', 'black d''hide vambraces',
    'blue d''hide body', 'blue d''hide chaps', 'blue d''hide shield',
    'blue d''hide vambraces', 'combat bracelet', 'combat bracelet(4)',
    'diamond bracelet', 'dragon axe', 'dragon battleaxe', 'dragon cane',
    'dragon chainbody', 'dragon dagger', 'dragon halberd',
    'dragon hasta', 'dragon longsword', 'dragon mace', 'dragon med helm',
    'dragon platelegs', 'dragon plateskirt', 'dragon scimitar', 'dragon spear',
    'dragon sq shield', 'dragon sword', 'dragonstone ring', 'earth battlestaff',
    'fire battlestaff', 'green d''hide body', 'green d''hide chaps',
    'green d''hide shield', 'green d''hide vambraces', 'lava battlestaff',
    'magic longbow', 'mithril platebody', 'mud battlestaff', 'mystic air staff',
    'mystic boots', 'mystic boots (dark)', 'mystic boots (light)',
    'mystic earth staff', 'mystic fire staff', 'mystic gloves',
    'mystic gloves (dark)', 'mystic gloves (light)', 'mystic hat',
    'mystic hat (dark)', 'mystic hat (light)', 'mystic lava staff',
    'mystic mud staff', 'mystic robe bottom', 'mystic robe bottom (dark)',
    'mystic robe bottom (light)', 'mystic robe top', 'mystic robe top (dark)',
    'mystic robe top (light)', 'mystic water staff',
    'noted abyssal bracelet(5)', 'noted adamant platebody',
    'noted air battlestaff', 'noted amulet of glory',
    'noted amulet of glory(4)', 'noted black d''hide body',
    'noted black d''hide chaps', 'noted black d''hide shield',
    'noted black d''hide vambraces', 'noted blue d''hide body',
    'noted blue d''hide chaps', 'noted blue d''hide shield',
    'noted blue d''hide vambraces', 'noted combat bracelet',
    'noted combat bracelet(4)', 'noted diamond bracelet',
    'noted dragon axe', 'noted dragon battleaxe', 'noted dragon cane',
    'noted dragon chainbody', 'noted dragon dagger', 'noted dragon dagger(p+)',
    'noted dragon halberd', 'noted dragon hasta', 'noted dragon longsword',
    'noted dragon mace', 'noted dragon med helm', 'noted dragon platelegs',
    'noted dragon plateskirt', 'noted dragon scimitar', 'noted dragon spear',
    'noted dragon sq shield', 'noted dragon sword', 'noted dragonstone ring',
    'noted earth battlestaff', 'noted fire battlestaff',
    'noted green d''hide body', 'noted green d''hide chaps',
    'noted green d''hide shield', 'noted green d''hide vambraces',
    'noted lava battlestaff', 'noted magic longbow', 'noted magic longbow (u)',
    'noted magic shortbow (u)', 'noted maple longbow (u)',
    'noted maple shortbow (u)', 'noted mithril platebody',
    'noted mud battlestaff', 'noted mystic air staff', 'noted mystic boots',
    'noted mystic boots (dark)', 'noted mystic boots (light)',
    'noted mystic earth staff', 'noted mystic fire staff',
    'noted mystic gloves', 'noted mystic gloves (dark)',
    'noted mystic gloves (light)', 'noted mystic hat',
    'noted mystic hat (dark)', 'noted mystic hat (light)',
    'noted mystic lava staff', 'noted mystic mud staff',
    'noted mystic robe bottom', 'noted mystic robe bottom (dark)',
    'noted mystic robe bottom (light)', 'noted mystic robe top',
    'noted mystic robe top (dark)', 'noted mystic robe top (light)',
    'noted mystic water staff', 'noted red d''hide body',
    'noted red d''hide chaps', 'noted red d''hide shield',
    'noted red d''hide vambraces', 'noted ring of wealth',
    'noted ring of wealth (4)', 'noted rune 2h sword', 'noted rune axe',
    'noted rune battleaxe', 'noted rune boots', 'noted rune cane',
    'noted rune chainbody', 'noted rune claws', 'noted rune crossbow',
    'noted rune dagger', 'noted rune dagger(p+)', 'noted rune full helm',
    'noted rune halberd', 'noted rune hasta', 'noted rune kiteshield',
    'noted rune longsword', 'noted rune mace', 'noted rune med helm',
    'noted rune pickaxe', 'noted rune platebody', 'noted rune platelegs',
    'noted rune plateskirt', 'noted rune scimitar', 'noted rune spear',
    'noted rune sq shield', 'noted rune sword', 'noted rune warhammer',
    'noted runite crossbow (u)', 'noted runite limbs', 'noted skills necklace',
    'noted skills necklace(4)', 'noted steel platebody',
    'noted water battlestaff', 'noted yew longbow', 'noted yew longbow (u)',
    'noted yew shortbow (u)', 'onyx bolts (e)', 'red d''hide body',
    'red d''hide chaps', 'red d''hide shield', 'red d''hide vambraces',
    'ring of wealth', 'ring of wealth (4)', 'rune 2h sword', 'rune axe',
    'rune battleaxe', 'rune boots', 'rune cane', 'rune chainbody', 'rune claws',
    'rune crossbow', 'rune dagger', 'rune dagger(p+)', 'rune full helm',
    'rune halberd', 'rune hasta', 'rune kiteshield', 'rune longsword',
    'rune mace', 'rune med helm', 'rune pickaxe', 'rune platebody',
    'rune platelegs', 'rune plateskirt', 'rune scimitar', 'rune spear',
    'rune sq shield', 'rune sword', 'rune warhammer', 'runite crossbow (u)',
    'runite limbs', 'skills necklace', 'skills necklace(4)', 'steel platebody',
    'water battlestaff', 'yew longbow'
  ];
end;

procedure TRSAlchHandler.LoadItems();
var
  i: Int32;
begin
  for i := 0 to Self.Config.GetArray('items').High() do
    Self.ItemArray += Self.Config.GetArray('items').getString(i);
end;

procedure TRSAlchHandler.Setup();
begin
  Self.Name := 'Alcher';

  if Self.ItemArray <> [] then
    Exit;

  Self.Config.Setup('alchhandler');
  if not Self.Config.Has('items', False) then
  begin
    Self.AddDefault();
    Self.SaveConfig();
    Exit;
  end;

  Self.LoadItems();
end;


function TRSAlchHandler.GetAlchItem(): TRSItem;
var
  match: TRSItemFinderMatch;
  itemStr: String;
begin
  if not Inventory.Open() and not Inventory.SetSelectedSlot(-1) then
    Exit;

  if not ItemFinder.Find(Self.ItemArray, Inventory.GetSlotBoxes(), match) then
  begin
    Self.DebugLn('No items in the alch list were found!');
    Self.Disabled := True;
    Exit;
  end;

  Result := match.Item;
  itemStr := Result;

  Self.DebugLn('Found item: ' + itemStr);

  itemStr := itemStr.After('noted ');
  Self.LowProfit  := ItemData.GetLowAlchProfit(itemStr);
  Self.HighProfit := ItemData.GetHighAlchProfit(itemStr);
end;

procedure TRSAlchHandler.AddProfit(hi: Boolean = True);
begin
  if hi then
    Self.TotalProfit += Self.HighProfit
  else
    Self.TotalProfit += Self.LowProfit;
end;

function TRSAlchHandler.ClickEmptySlot(slot: Int32 = -1): Boolean;
var
  slots: TIntegerArray;
begin
  slots := Inventory.GetEmptySlots();
  Result := slots <> [];

  if Result then
  begin
    if slot = -1 then
      slot := Inventory.Items.PointToSlot(Mouse.Position());

    if slot = -1 then
      Inventory.Items.MouseRandomSlot(slots)
    else
    begin
      if (slots.Find(slot) = -1) or Antiban.BioDice(50) then
        slot := Inventory.RandomSlotNearby(slot, slots);
      Inventory.MouseSlot(slot);
    end;
    Mouse.Click(MOUSE_LEFT);
  end;
end;

function TRSAlchHandler.DisableCast(): Boolean;
var
  i, attempt: Int32;
begin
  if Inventory.Count() < 28 then
    i := SRL.TruncatedGauss(0, 5)
  else
    i := SRL.TruncatedGauss(3, 5);

  repeat
    if Inc(attempt) > 10 then
      Self.Fatal('Cannot disable the alchemy spell!');

    case i of
      0..2: Self.ClickEmptySlot();
      3: Mouse.Click(MainScreen.Bounds, MOUSE_LEFT);
      4: Mouse.Click(Chat.Bounds, MOUSE_LEFT);
      5: ChooseOption.Select('Cancel');
    end;
  until not MainScreen.IsUpText('Alchemy -');
  Result := True;
end;


function TRSAlchHandler.SelectSpell(hi: Boolean = True): Boolean;
var
  attempt: Int32;
  spellStr: String;
  spell: ERSSpell;
begin
  if hi then
  begin
    spell := ERSSpell.HIGH_LEVEL_ALCHEMY;
    spellStr := 'h Level Alchemy -';
  end
  else
  begin
    spell := ERSSpell.LOW_LEVEL_ALCHEMY;
    spellStr := 'w Level Alchemy -';
  end;

  repeat
    if not Magic.IsSelected(spell) or
       not GameTabs.FKeyOpen(ERSGameTab.INVENTORY)
    then
      Magic.CastSpell(spell);

    Result := MainScreen.IsUpText(spellStr) or Inventory.IsOpen();
  until Result or (Inc(attempt) > 3);

  Self.Disabled := not Result;
end;

function TRSAlchHandler.SelectItem(hi: Boolean = True; waitTimer: Boolean = False): Boolean;
var
  attempt, slot, spellSlot: Int32;
  box: TBox;
  slots: TIntegerArray;
begin
  if hi then
    Self.Timer.Length := 5 * 600
  else
    Self.Timer.Length := 3 * 600;

  spellSlot := Self.SlotUnder;

  for attempt := 0 to 2 do
  begin
    if Self.Item.IsNull() then
      Self.Item := Self.GetAlchItem();

    if Self.Item.IsNull() then
      Exit;

    if Inventory.FindItem(Self.Item, slot) then
    begin
      if (slot <> spellSlot) and (Inventory.Items.CountStack(slot) > 0) then
      begin
        Self.DisableCast();
        if Inventory.Items.Move(slot, spellSlot) then
          Exit;
        Break;
      end;

      if SRL.Dice(99.5) then
      begin
        if not Inventory.MouseSlot(slot) and not MainScreen.IsUpText('>') then
          Exit;

        if not waitTimer then
        begin
          if not Self.Timer.IsFinished() and (Self.Timer.Timeout > 0) then
            Exit;
        end else
          WaitUntil(Self.Timer.IsFinished(), 50, 3100);

        Mouse.Click(MOUSE_LEFT);

        if not WaitUntil(XPBar.EarnedXP() or Magic.IsOpen(), 300, 3000) then
          Exit;

        Self.Timer.Restart();
        Self.AddProfit(hi);
        Exit(True);
      end;

      slots := Inventory.GetEmptySlots();
      if slots <> [] then
      begin
        slot := Inventory.RandomSlotNearby(slot, slots);
        Inventory.ClickSlot(slot, '>');
      end;
      Exit;
    end
    else if ItemData.GetDefinitionBoolean(Self.Item, 'equipable') then
    begin
      Self.DisableCast();
      if Equipment.FindItem(Self.Item, box) then
      begin
        Mouse.Click(box, MOUSE_LEFT);
        Exit;
      end;
    end;

    Self.Item := '';
  end;
end;


function TRSAlchHandler.CastAlchemy(hi: Boolean = True; waitTimer: Boolean = False): Boolean;
var
  attempt: Int32;
begin
  XPBar.EarnedXP(); //Update cached XP.

  repeat
    if not Self.SelectSpell(hi) then
      Exit;
    Result := Self.SelectItem(hi, waitTimer);
    TRSWalkerObject.RedClicked := False; //Integration with other wasplib components.
  until Result or (Inc(attempt) > 2);
end;

var
  RSAlchHandler: TRSAlchHandler;

procedure TSRL.Setup(); override;
begin
  inherited;

  RSAlchHandler.Setup();
end;

function TRSMagic.FindSpell(spell: ERSSpell; out box: TBox; attempts: Int32 = 2): Boolean; override;
var
  slots: TBoxArray;
  i: Int32;
begin
  Result := inherited(spell, box, attempts);

  if not Result then
    Exit;

  if (spell <> ERSSpell.LOW_LEVEL_ALCHEMY) and
     (spell <> ERSSpell.HIGH_LEVEL_ALCHEMY) then
    Exit;

  slots := Inventory.GetSlotBoxes();
  for i := 0 to High(slots) do
    if box.Overlap(slots[i]) then
    begin
      RSAlchHandler.SlotUnder := i;
      Exit;
    end;
end;

function TRSMagic.MouseSpell(spell: ERSSpell): Boolean; override;
var
  box: TBox;
  upText: String;
begin
  if (Self.SpellCache[spell].UpText <> '') and
     Self.SpellCache[spell].Box.Contains(Mouse.Position()) then
  begin
    upText := MainScreen.GetUpText().After('Cast').Before('/').Strip();

    if (upText = Self.SpellCache[spell].UpText) then
      Exit(True);
    if upText.Contains('>') then
    begin
      ChooseOption.Select('Cancel');
      Exit(Self.MouseSpell(spell));
    end;
  end;

  Result := Self.Open() and Self.FindSpell(spell, box);

  if Result then
  begin
    Mouse.Move(box);

    if Self.SpellCache[spell].UpText = '' then
    begin
      upText := MainScreen.GetUpText().After('Cast').Before('/').Strip();
      Self.SpellCache[spell].UpText := upText;
    end;
  end;
end;

function TScriptForm.CreateAlchemyPanel(): TTabSheet;
  type TListBox = TListBox;
  procedure TListBox._AddSorted(str: String);
  var
    i, count: Int32;
  begin
    count := Self.GetCount();
    for i := 0 to count do
    begin
      if i < count - 1 then
      begin
        if str > Self.GetItems().GetStrings(i) then
          Continue;

        Self.GetItems().Insert(i, str);
        Exit;
      end;
    end;

    Self.GetItems().Add(str);
  end;

  procedure TListBox._Delete(i: Int32; setIndex: Boolean = True);
  var
    count: Int32;
  begin
    Self.GetItems().Delete(i);

    if not setIndex then Exit;

    count := Self.GetCount() - 1;
    if i > count then
      Self.SetItemIndex(count)
    else
      Self.SetItemIndex(i);
  end;

  procedure TListBox._ReWriteAlchConfig();
  var
    i, count: Int32;
  begin
    RSAlchHandler.ItemArray := [];
    count := Self.GetCount() - 1;
    for i := 0 to count do
      RSAlchHandler.ItemArray += Self.getItems().GetStrings(i);
    RSAlchHandler.SaveConfig();
  end;

  type TScriptForm = TScriptForm;
  procedure TScriptForm._AddAlchList(sender: TObject);
  var
    itemList, alchList: TListBox;
    i: Int32;
    str, tmp: String;
  begin
    tmp := TButton(sender).getCaption();
    TButton(sender).setCaption('Loading...');

    itemList := Self.Form.GetChild('item_list_listbox');
    alchList := Self.Form.GetChild('alch_list_listbox');

    i := itemList.getItemIndex();
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      Exit;
    end;

    str := itemList.GetSelectedText();
    itemList._Delete(i);
    alchList._AddSorted(str);
    alchList._ReWriteAlchConfig();

    TButton(sender).setCaption(tmp);
  end;

  procedure TScriptForm._AddBothAlchList(sender: TObject);
  var
    itemList, alchList: TListBox;
    i: Int32;
    str, tmp: String;
  begin
    tmp := TButton(sender).getCaption();
    TButton(sender).setCaption('Loading...');
    itemList := Self.Form.GetChild('item_list_listbox');
    alchList := Self.Form.GetChild('alch_list_listbox');

    i := itemList.getItemIndex();
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      Exit;
    end;

    str := itemList.GetSelectedText();
    itemList._Delete(i);
    alchList._AddSorted(str);

    if str.Contains('noted ') then
      str := str.After('noted ')
    else
      str := 'noted ' + str;

    i := itemList.getItems().IndexOf(str);
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      alchList._ReWriteAlchConfig();
      Exit;
    end;

    itemList._Delete(i, False);
    alchList._AddSorted(str);
    alchList._ReWriteAlchConfig();

    TButton(sender).setCaption(tmp);
  end;


  procedure TScriptForm._RemoveAlchList(sender: TObject);
  var
    itemList, alchList: TListBox;
    i: Int32;
    str, tmp: String;
  begin
    tmp := TButton(sender).getCaption();
    TButton(sender).setCaption('Loading...');

    itemList := Self.Form.GetChild('item_list_listbox');
    alchList := Self.Form.GetChild('alch_list_listbox');

    i := alchList.getItemIndex();
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      Exit;
    end;

    str := alchList.GetSelectedText();
    alchList._Delete(i);
    itemList._AddSorted(str);
    alchList._ReWriteAlchConfig();
    TButton(sender).setCaption(tmp);
  end;

  procedure TScriptForm._RemoveBothAlchList(sender: TObject);
  var
    itemList, alchList: TListBox;
    i: Int32;
    str, tmp: String;
  begin
    tmp := TButton(sender).getCaption();
    TButton(sender).setCaption('Loading...');

    itemList := Self.Form.GetChild('item_list_listbox');
    alchList := Self.Form.GetChild('alch_list_listbox');

    i := alchList.getItemIndex();
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      Exit;
    end;

    str := alchList.GetSelectedText();
    alchList._Delete(i);
    itemList._AddSorted(str);

    if str.Contains('noted ') then
      str := str.After('noted ')
    else
      str := 'noted ' + str;

    i := alchList.getItems().IndexOf(str);
    if i = -1 then
    begin
      TButton(sender).setCaption(tmp);
      alchList._ReWriteAlchConfig();
      Exit;
    end;

    alchList._Delete(i, False);
    itemList._AddSorted(str);
    alchList._ReWriteAlchConfig();

    TButton(sender).setCaption(tmp);
  end;


  procedure TScriptForm._OnItemListSelected({$H-}sender: TObject);{$H+}
  var
    btn: TButton;
  begin
    btn := Self.Form.GetChild('add_alch_button');
    btn.setEnabled(True);
    btn := Self.Form.GetChild('add_both_alch_button');
    btn.setEnabled(True);
  end;

  procedure TScriptForm._OnAlchListSelected({$H-}sender: TObject);{$H+}
  var
    btn: TButton;
  begin
    btn := Self.Form.GetChild('remove_alch_button');
    btn.setEnabled(True);
    btn := Self.Form.GetChild('remove_both_alch_button');
    btn.setEnabled(True);
  end;

  procedure TScriptForm._OnAlchListClear(sender: TObject);
  var
    itemList, alchList: TListBox;
    itemNames: TStringList;
    i: Int32;
    str, tmp: String;
  begin
    tmp := TButton(sender).getCaption();
    TButton(sender).setCaption('Loading...');

    itemList := Self.Form.GetChild('item_list_listbox');
    alchList := Self.Form.GetChild('alch_list_listbox');

    alchList.Clear();
    alchList._ReWriteAlchConfig();

    itemNames.Init();
    itemNames.Assign(ItemFinder.Database.GetColumn('item'));
    itemNames.setSorted(True);

    itemList.Clear();
    for i := 0 to itemNames.getCount()-1 do
    begin
      if str = itemNames.getStrings(i).Before('=') then
        Continue; //skip duplicates
      str := itemNames.getStrings(i).Before('=');
      itemList.GetItems().Add(str);
    end;
    itemNames.Free();
    TButton(sender).setCaption(tmp);
  end;

  procedure TScriptForm.OnItemSearchChange(sender: TObject);
  var
    list: TListBox;
  begin
    list := Self.Form.GetChild('item_list_listbox');
    if not TEdit(sender).IsEmpty() then
      if list.Search(TEdit(sender).getText()) > -1 then
        Self._OnItemListSelected(list);
  end;

  procedure TScriptForm.OnAlchSearchChange(sender: TObject);
  var
    list: TListBox;
  begin
    list := Self.Form.GetChild('alch_list_listbox');
    if not TEdit(sender).IsEmpty() then
      if list.Search(TEdit(sender).getText()) > -1 then
        Self._OnItemListSelected(list);
  end;

var
  itemSearch, alchSearch: TLabeledEdit;
  itemList, alchList: TLabeledListBox;
  itemNames: TStringList;
  fullWidth, space, y, i: Int32;
  str:String;
  button: TButton;
begin
  Self.AddTab('Alchemy Settings');
  Result := Self.Tabs[High(Self.Tabs)];

  fullWidth := Floor(Self.Size.X/3);
  space := Floor(fullWidth * 0.09);
  y := Floor(Self.Size.Y/10);

  with itemSearch do
  begin
    Create(Result);
    SetCaption('Search item:');
    SetTooltip('Search for an item.');
    SetName('item_search');
    SetLeft(space);
    SetTop(y - TControl.AdjustToDPI(15));
    SetWidth(fullWidth);
    Edit.setOnChange(@Self.OnItemSearchChange);
  end;

  with itemList do
  begin
    Create(Result);
    SetCaption('Item list:');
    SetTooltip('List of all items on osrs currently supported.');
    SetName('item_list');
    SetLeft(space);
    SetTop(y + TControl.AdjustToDPI(35));
    SetWidth(itemSearch.GetWidth());
    SetHeight(Floor(Self.Size.Y/1.45));

    itemNames.Init();
    itemNames.Assign(ItemFinder.Database.GetColumn('item'));
    itemNames.setSorted(True);

    ListBox.getItems().BeginUpdate();
    for i := 0 to itemNames.getCount()-1 do
    begin
      if str = itemNames.getStrings(i).Before('=') then
        Continue; //skip duplicates
      str := itemNames.getStrings(i).Before('=');
      if not RSAlchHandler.ItemArray.Contains(str) then
        AddItem(str);
    end;
    ListBox.getItems().EndUpdate();

    itemNames.Free();
    ListBox.setOnClick(@Self._OnItemListSelected);
  end;

  with alchSearch do
  begin
    Create(Result);
    SetCaption('Search alch:');
    SetTooltip('Search for an alch.');
    SetName('alch_search');
    SetLeft(Self.Size.X - space - fullWidth);
    SetTop(itemSearch.GetTop());
    SetWidth(itemSearch.GetWidth());
    Edit.setOnChange(@Self.OnAlchSearchChange);
  end;

  with alchList do
  begin
    Create(Result);
    SetCaption('Alch list:');
    SetTooltip('List of all items that will be looked for when WaspLib alch handler is used. For performance, keep the list short.');
    SetName('alch_list');
    SetLeft(alchSearch.GetLeft());
    SetTop(itemList.GetTop());
    SetWidth(itemList.GetWidth());
    SetHeight(itemList.GetHeight());

    ListBox.getItems().BeginUpdate();
    for i := 0 to High(RSAlchHandler.ItemArray) do
      AddItem(RSAlchHandler.ItemArray[i]);
    ListBox.getItems().EndUpdate();
    ListBox.setOnClick(@Self._OnAlchListSelected);
  end;


  with button do
  begin
    Create(Result);
    setCaption('Add Item >>>');
    SetName('add_alch_button');
    SetLeft(itemList.GetRight() + space);
    SetTop(y*2);
    setWidth(space*7);
    SetHeight(TControl.AdjustToDPI(30));
    setOnClick(@Self._AddAlchList);
    setEnabled(False);
  end;

  with button do
  begin
    Create(Result);
    setCaption('Add Noted/Unnoted >>');
    SetName('add_both_alch_button');
    SetLeft(itemList.GetRight() + space);
    SetTop(y*3);
    setWidth(space*7);
    SetHeight(TControl.AdjustToDPI(30));
    setOnClick(@Self._AddBothAlchList);
    setEnabled(False);
  end;


  with button do
  begin
    Create(Result);
    setCaption('<< Remove Noted/Unnoted');
    SetName('remove_both_alch_button');
    SetLeft(itemList.GetRight() + space);
    SetTop(y*5);
    setWidth(space*7);
    SetHeight(TControl.AdjustToDPI(30));
    setOnClick(@Self._RemoveBothAlchList);
    setEnabled(False);
  end;

  with button do
  begin
    Create(Result);
    setCaption('<<< Remove Item');
    SetName('remove_alch_button');
    SetLeft(itemList.GetRight() + space);
    SetTop(y*6);
    setWidth(space*7);
    SetHeight(TControl.AdjustToDPI(30));
    setOnClick(@Self._RemoveAlchList);
    setEnabled(False);
  end;


  with button do
  begin
    Create(Result);
    setCaption('Clear list');
    SetTooltip('Clear the alch list. If it''s empty, restarting the script will re-add the default one.');
    SetName('clear_alch_button');
    SetLeft(itemList.GetRight() + space);
    SetTop(y*8);
    setWidth(space*7);
    SetHeight(TControl.AdjustToDPI(30));
    setOnClick(@Self._OnAlchListClear);
  end;
end;


