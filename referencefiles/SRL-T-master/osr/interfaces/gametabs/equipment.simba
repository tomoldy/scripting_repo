(*
# Equipment
Methods to interact with the Equipment tab.
*)
{$DEFINE SRL_EQUIPMENT_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

type
(*
(ERSEquipmentSlot)=
## type ERSEquipmentSlot
```pascal
ERSEquipmentSlot = (
  HEAD, SECONDARY_AMMO, CAPE, NECK, AMMO, WEAPON,
  BODY, SHIELD, LEGS, HANDS, FEET, RING
);
```
Enumerator of the equipment slots.
*)
  ERSEquipmentSlot = (
    HEAD, SECONDARY_AMMO, CAPE, NECK, AMMO, WEAPON,
    BODY, SHIELD, LEGS, HANDS, FEET, RING
  );

(*
(ERSEquipmentButton)=
## type ERSEquipmentButton
```pascal
ERSEquipmentButton = (STATS, PRICES, DEATH, FOLLOWER);
```
Enumerator of the equipment buttons.
*)
  ERSEquipmentButton = (STATS, PRICES, DEATH, FOLLOWER);

(*
(TRSEquipment)=
## type TRSEquipment
```pascal
TRSEquipment = type TRSItemInterface;
```
Main equipment type reponsible for handling it.
*)
  TRSEquipment = type TRSItemGameTab;


(*
## Equipment.GetSlotBoxes
```pascal
function TRSEquipment.GetSlotBoxes(): TBoxArray;
```
Returns the boxes of slots in the equipment tab.

Example:
```pascal
Debug(Equipment.GetSlotBoxes());
```
*)
function TRSEquipment.GetSlotBoxes(): TBoxArray;
begin
  Result := Self.SlotBoxes;
end;

(*
## Equipment.GetSlotBox
```pascal
function TRSEquipment.GetSlotBox(slot: ERSEquipmentSlot): TBox;
function TRSEquipment.GetSlotBox(slot: Int32): TBox;
```
Returns the TBox of the specified equipment slot.

Example:
```pascal
WriteLn(Equipment.GetSlotBox(ERSEquipmentSlot.HEAD));
WriteLn(Equipment.GetSlotBox(0)); // HEAD slot
```
*)
function TRSEquipment.GetSlotBox(slot: ERSEquipmentSlot): TBox;
begin
  if InRange(Ord(slot), 0, Ord(High(ERSEquipmentSlot))) then
    Result := Self.GetSlotBoxes()[Ord(slot)];
end;

function TRSEquipment.GetSlotBox(slot: Int32): TBox; overload;
begin
  if InRange(slot, 0, Ord(High(ERSEquipmentSlot))) then
    Result := Self.GetSlotBoxes()[slot];
end;

procedure TRSEquipment.Setup(); override;
begin
  inherited;

  Self.Name := 'Equipment';
  Self.Items.Name := Self.Name + '.ItemInterface';
  Self.Items.SlotBoxesFunction := @Self.GetSlotBoxes;
  Self.Items.DiscoverOffset := [2, 11, 2, -2];
end;

procedure TRSEquipment.SetupAlignment(); overload;
var
  alignment: TRSInterfaceAlignment;
begin
  alignment.Left := GameTab.Bounds.X1;
  alignment.Right := GameTab.Bounds.X2;
  alignment.Top := GameTab.Bounds.Y1;
  alignment.Bottom := GameTab.Bounds.Y2;

  Self.SetupAlignment(alignment);

  Self.SlotBoxes := Grid(3,2, 35, 35, [6, 4], [Self.Bounds.X1+33, Self.Bounds.Y1+5]) +
                    Grid(3,3, 35, 35, [21, 5], [Self.Bounds.X1+18, Self.Bounds.Y1+83]);

  Delete(Self.SlotBoxes, 0, 1);
  Delete(Self.SlotBoxes, 8, 1);
  Delete(Self.SlotBoxes, 9, 1);
end;


(*
## Equipment.IsOpen
```pascal
function TRSEquipment.IsOpen(): Boolean;
```
Returns true/false whether the equipment tab is open or not.

Example:
```pascal
WriteLn Equipment.IsOpen();
```
*)
function TRSEquipment.IsOpen(): Boolean;
begin
  Result := GameTabs.GetCurrentTab() = ERSGameTab.EQUIPMENT;
end;

(*
## Equipment.Open
```pascal
function TRSEquipment.Open(): Boolean;
```
Attempts to open the equipment tab.
Returns true if we succeed.

Example:
```pascal
WriteLn Equipment.Open();
```
*)
function TRSEquipment.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.EQUIPMENT);
end;


(*
## Equipment.GetButtons
```pascal
function TRSEquipment.GetButtons(): TRSButtonArray;
```
Returns a `TRSButtonArray` of the bottom equipment buttons.

Example:
```pascal
Debug(Equipment.GetButtons());
```
*)
function TRSEquipment.GetButtons(): TRSButtonArray;
begin
  Result := Self.FindButtons([[40,40]]);
end;

(*
## Equipment.GetButton
```pascal
function TRSEquipment.GetButton(button: ERSEquipmentButton): TRSButton;
```
Returns a `TRSButton` of the specified equipment button.

Example:
```pascal
Debug([Equipment.GetButton(ERSEquipmentButton.PRICES)]);
```
*)
function TRSEquipment.GetButton(button: ERSEquipmentButton): TRSButton;
var
  buttons: TRSButtonArray;
begin
  buttons := Self.GetButtons();
  if Length(buttons) = Length(ERSEquipmentButton) then
    Result := buttons[button];
end;


(*
## Equipment.FindItem
```pascal
function TRSEquipment.FindItem(item: TRSItem; out bounds: TBox): Boolean;
```
Returns true if we find the `item` specified in the equipment tab.
`bounds` will return a `TBox` of the item bounds.

Example:
```pascal
if Equipment.FindItem('Abyssal whip', bounds) then
  Debug(bounds);
```
*)
function TRSEquipment.FindItem(item: TRSItem; out bounds: TBox): Boolean;
begin
  if Self.Open() then
    Result := Self.Items.Find([item], bounds);
end;

(*
## Equipment.FindItems
```pascal
function TRSEquipment.FindItems(Items: TRSItemArray; out Slots: TIntegerArray): Boolean;
```

Attempts to find the items passed in **Items**. The function returns true if any item is found and
the slots of the found items will be returned in **Slots**.

Example:
```pascal
var
  slots: TIntegerArray;
begin
  if Equipment.FindItems(['Slayer helmet', 'Dragon scimitar'], slots) then
    WriteLn('The slots that have Slayer helmet and Dragon scimitar are: ', slots);
end;
```
*)
function TRSEquipment.FindItems(items: TRSItemArray; out slots: TIntegerArray): Boolean;
begin
  if not Self.Open() then Exit;
  slots := Self.Items.IndicesOf(items);
  Result := Length(slots) > 0;
end;

(*
## Equipment.Contains
```pascal
function TRSEquipment.ContainsItem(item: TRSItem): Boolean;
function TRSEquipment.ContainsAny(items: TRSItemArray): Boolean;
function TRSEquipment.ContainsAll(items: TRSItemArray): Boolean;
```
Same as {ref}`Equipment.FindItem` without the bounds.

Example:
```pascal
WriteLn Equipment.ContainsItem('Abyssal whip');
```
*)
function TRSEquipment.ContainsItem(Item: TRSItem): Boolean;
begin
  if Self.Open() then
    Result := Self.Items.Contains(item);
end;

(*
## Equipment.ContainsAny
```pascal
function TRSEquipment.ContainsAny(items: TRSItemArray): Boolean;
```
Returns true if any item on the `items` parameter is found on the equipment tab.

Example:
```pascal
WriteLn Equipment.ContainsAny(['Abyssal whip', 'Dragon dagger']);
```
*)
function TRSEquipment.ContainsAny(items: TRSItemArray): Boolean;
begin
  if Self.Open() then
    Result := Self.Items.ContainsAny(items);
end;

(*
## Equipment.ContainsAll
```pascal
function TRSEquipment.ContainsAll(items: TRSItemArray): Boolean;
```
Returns true if all items on the `items` parameter are found on the equipment tab.

Example:
```pascal
WriteLn Equipment.ContainsAll(['Abyssal whip', 'Dragon dagger']);
```
*)
function TRSEquipment.ContainsAll(items: TRSItemArray): Boolean;
begin
  if Self.Open() then
    Result := Self.Items.ContainsAll(items);
end;


(*
## Equipment.DiscoverAll
```pascal
function TRSEquipment.DiscoverAllEx(): array of TRSItemArray;
function TRSEquipment.DiscoverAll(): TRSItemArray;
```
Returns all possible items found in the equipment.
The extended version returns an `array of TRSItemArray` while the other returns
a flat `TRSItemArray`.

Example:
```pascal
if Equipment.Open() then
  WriteLn Equipment.DiscoverAll();
```
*)
function TRSEquipment.DiscoverAllEx(): array of TRSItemArray;
begin
  if Self.Open() then
    Result := Self.Items.DiscoverAllEx();
end;

function TRSEquipment.DiscoverAll(): TRSItemArray;
begin
  if Self.Open() then
    Result := Self.Items.DiscoverAll();
end;

(*
## Equipment.Discover
```pascal
function TRSEquipment.Discover(slot: ERSEquipmentSlot): TRSItemArray;
function TRSInventory.Discover(slot: Int32): TRSItemArray;
```
Discovers what item is on the specified equipment `slot`.

Example:
```pascal
if Equipment.Open() then
  WriteLn Equipment.Discover(0);
```
*)
function TRSEquipment.Discover(slot: ERSEquipmentSlot): TRSItemArray;
begin
  if Self.Open() then
    Result := Self.Items.Discover(Ord(slot));
end;

function TRSEquipment.Discover(slot: Int32): TRSItemArray; overload;
begin
  if Self.Open() then
    Result := Self.Items.Discover(slot);
end;


(*
## Equipment.HoverItem
```pascal
function TRSEquipment.HoverItem(item: TRSItem): Boolean;
```
Attempts to hover an item, returns false if the item is not on the equipment tab.

Example:
```pascal
WriteLn Equipment.HoverItem('Abyssal whip');
```
*)
function TRSEquipment.HoverItem(item: TRSItem): Boolean;
begin
  Result := Self.Open() and Self.Items.MouseItem(item);
end;

(*
## Equipment.ClickItem
```pascal
function TRSEquipment.ClickItem(item: TRSItem; option: String = ''): Boolean;
```
Attempts to click an item, if an `option` is specified, that `option` will be
selected if it exists via right clicking if it's not the first action.

Returns false if the item is not found or the option specified doesn't exist.

Example:
```pascal
WriteLn Equipment.ClickItem('Abyssal whip');
```
*)
function TRSEquipment.ClickItem(item: TRSItem; option: String = ''): Boolean;
begin
  Result := Self.Open() and Self.Items.InteractItem(item, option);
end;

(*
## Equipment.CountItemStack
```pascal
function TRSEquipment.CountItemStack(item: TRSItem): Int32;
```
Attempts to count an item stack.
Realistically, it's only useful for ammo or any other few stackable items that
can be equipped.

Returns `-1` if the item is not found or if there's no stack number to read.

Example:
```pascal
WriteLn Equipment.CountItemStack('Broad bolts');
```
*)
function TRSEquipment.CountItemStack(item: TRSItem): Int32;
begin
  if Self.IsOpen() then
    Result := Self.Items.CountStack(item);
end;

(*
## Equipment.ClickSlot
```pascal
function TRSEquipment.ClickSlot(slot: Int32; option: String = ''): Boolean;
function TRSEquipment.ClickSlot(slot: ERSEquipmentSlot; option: String = ''): Boolean; overload;
```
Moves the mouse and clicks on the specified equipment slot. Slot is an Integer corresponding to the equipment slot indices.
If option is empty, the slot is left clicked. Otherwise, a right click is performed
and the specified option is selected from the context menu.

Example:
```pascal
if Equipment.ClickSlot(ERSEquipmentSlot.WEAPON) then
  WriteLn('Left clicked the weapon slot');

if Equipment.ClickSlot(ERSEquipmentSlot.RING, 'Rub') then
  WriteLn('Right clicked and selected "Rub" on the ring slot');
```
*)
function TRSEquipment.ClickSlot(slot: Int32; option: String = ''): Boolean;
begin
  Result := Self.Open() and Self.Items.InteractSlot(slot, option);
end;

function TRSEquipment.ClickSlot(slot: ERSEquipmentSlot; option: String = ''): Boolean; overload;
begin
  Result := Self.ClickSlot(Ord(slot), option);
end;

(*
## Equipment.IsSlotUsed
```pascal
function TRSEquipment.IsSlotUsed(b: TBox): Boolean;
function TRSEquipment.IsSlotUsed(slot: ERSEquipmentSlot): Boolean; overload;
```
Returns true if a slot is occupied.

Example:
```pascal
WriteLn Equipment.IsSlotUsed(ERSEquipmentSlot.WEAPON);
```
*)
function TRSEquipment.IsSlotUsed(b: TBox): Boolean;
begin
  // black with tolerance for when item is transparent
  Result := SRL.CountColor(CTS1(1118740, 35), b) > 0;
end;

function TRSEquipment.IsSlotUsed(slot: ERSEquipmentSlot): Boolean; overload;
begin
  Result := Self.Open() and Self.Items.SlotUsed(Ord(slot));
end;


(*
## Equipment.CountGear
```pascal
function TRSEquipment.CountGear(): Int32;
```
Returns the number of equipped items.

Example:
```pascal
WriteLn Equipment.CountGear();
```
*)
function TRSEquipment.CountGear(): Int32;
begin
  if Self.Open() then
    Result := Self.Items.Count();
end;


procedure TRSEquipment.Draw(bitmap: TMufasaBitmap); override;
var
  buttons: TRSButtonArray;
begin
  if not Self.IsOpen() then
    Exit;

  inherited;

  bitmap.DrawBoxes(Self.GetSlotBoxes(), $00FFFF);
  buttons := Self.GetButtons();
  buttons.Draw(bitmap);
end;

var
(*
## var Equipment
Global Equipment variable.
*)
  Equipment: TRSEquipment;

procedure TRSClient.ClientModeChanged(); override;
begin
  inherited;
  Equipment.SetupAlignment();
end;

procedure TSRL.Setup(); override;
begin
  inherited;
  Equipment.Setup();
end;

procedure TSRL.Debug(bitmap: TMufasaBitmap); override;
begin
  inherited;
  Equipment.Draw(bitmap);
end;

