(*
# DotFilters
Methods to handle DotFilters. Dot filters are filters that filter in or out ERSMinimapDots.
*)

{$DEFINE SRL_DOTFILTERS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRl-T/osr.simba}
{$ENDIF}

type
(*
(TRSDotFilter)=
## type TRSDotFilter
```pascal
TRSDotFilter = record
  Bounds: TPointArray;
  Circle: TCircle;
  Inside: Boolean;

  Timeout: Boolean;
  Timer: TCountDown;
end;
```
Type used to filter minimap dots.
*)
  TRSDotFilter = record
    Bounds: TPointArray;
    Circle: TCircle;
    Inside: Boolean;

    Timeout: Boolean;
    Timer: TCountDown;
  end;


(*
## TRSDotFilter.Create
```pascal
function TRSDotFilter.Create(bounds: TPointArray; c: TCircle; inside: Boolean; time: Int32 = -1): TRSDotFilter; static;
```
Static method to create a TRSDotFilter.
*)
function TRSDotFilter.Create(bounds: TPointArray; c: TCircle; inside: Boolean; time: Int32 = -1): TRSDotFilter; static;
var
  timer: TCountDown;
begin
  timer.Init(time);
  Result := [bounds, c, inside, (time > 0), timer];
end;

(*
## TRSDotFilter.IsExpired
```pascal
function TRSDotFilter.IsExpired(): Boolean;
```
Checks if a TRSDotFilter is already expired by checking it's timer.
*)
function TRSDotFilter.IsExpired(): Boolean;
begin
  Result := Self.Timeout and Self.Timer.IsFinished();
end;

(*
## TRSDotFilter.IsValid
```pascal
function TRSDotFilter.IsValid(p: TPoint): Boolean;
```
Checks if a point is valid accordint to the current TRSDotFilter;
*)
function TRSDotFilter.IsValid(p: TPoint): Boolean;
var
  inPoly, inCircle: Boolean;
begin
  if Self.Bounds <> [] then inPoly := SRL.PointInPoly(p, Self.Bounds);
  if Self.Circle <> [] then inCircle := Self.Circle.Contains(p);

  if Self.Inside then
    Result := inPoly or inCircle
  else
    Result := not inPoly and not inCircle;
end;


(*
## TRSDotFilter.Draw
```pascal
procedure TRSDotFilter.Draw(out bitmap: TMufasaBitmap);
```
Internal method to draw the current dot filter on a bitmap for debugging.
You can see this in action by calling `Debug(TRSDotFilter)`.
*)
procedure TRSDotFilter.Draw(bitmap: TMufasaBitmap);
var
  color: Int32;
begin

  with Self do
  begin
    if Inside then color := $BE9625 else color := $FF;
    if (Bounds <> []) and Minimap.IsPointOn(Bounds.Mean(), Bounds.MinAreaCircle().Radius) then
      bitmap.DrawTPA(Bounds.Connect(), color);
    if (Circle <> []) and Minimap.IsPointOn(Circle.Mean(), Circle.Radius) then
      bitmap.DrawCircle(Circle.Mean(), Circle.Radius, color);
  end;
end;

type
(*
(TRSDotFilterArray)=
## type TRSDotFilterArray
Type used to manage and interact with arrays of TRSDotFilters.
*)
  TRSDotFilterArray = array of TRSDotFilter;

(*
## TRSDotFilterArray.Setup
```pascal
procedure TRSDotFilterArray.Setup(bounds: TPointArray; circle: TCircle; inside: Boolean; time: Int32 = -1);
procedure TRSDotFilterArray.SetupBounds(bounds: TPointArray; inside: Boolean; time: Int32 = -1);
procedure TRSDotFilterArray.SetupCircle(circle: TCircle; inside: Boolean; time: Int32 = -1);
```
Creates and adds a TRSDotFilter to the current TRSDotFilterArray.
*)
procedure TRSDotFilterArray.Setup(bounds: TPointArray; circle: TCircle; inside: Boolean; time: Int32 = -1);
begin
  Self += TRSDotFilter.Create(bounds, circle, inside, time);
end;

procedure TRSDotFilterArray.SetupBounds(bounds: TPointArray; inside: Boolean; time: Int32 = -1);
var
  c: TCircle;
begin
  Self.Setup(bounds, c, inside, time);
end;

procedure TRSDotFilterArray.SetupCircle(circle: TCircle; inside: Boolean; time: Int32 = -1);
var
  bounds: TPointArray := [];
begin
  Self.Setup(bounds, circle, inside, time);
end;


(*
## TRSDotFilterArray.ClearExpired
```pascal
procedure TRSDotFilterArray.ClearExpired();
```
Clears expired TRSDotFilter from the current TRSDotFilterArray.
You usually don't need to call this directly, but you can if you want.
Other methods already take care of this by default for you regardless.
*)
procedure TRSDotFilterArray.ClearExpired();
var
  i: Int32;
begin
  for i := High(Self) downto 0 do
    if Self[i].IsExpired() then
      Delete(Self, i, 1);
end;


(*
## TRSDotFilterArray.IsValid
```pascal
function TRSDotFilterArray.IsValid(dot: TPoint; clearExpired: Boolean = True): Boolean;
```
Checks if a dot/point is valid in any of the TRSDotFilters in the current array.
By default, expired TRSDotFilters are cleared from the array while this runs.
*)
function TRSDotFilterArray.IsValid(dot: TPoint; clearExpired: Boolean = True): Boolean;
var
  i: Int32;
begin
  for i := High(Self) downto 0 do
  begin
    if clearExpired and Self[i].IsExpired() then
    begin
      Delete(Self, i, 1);
      Continue;
    end;

    if Self[i].IsValid(dot) then
      Exit(True);
  end;
end;

(*
## TRSDotFilterArray.FilterDots
```pascal
function TRSDotFilterArray.FilterDots(dots: TPointArray): TPointArray;
```
Filter the dots/points passed into this according to our current TRSDotFilters.
*)
function TRSDotFilterArray.FilterDots(dots: TPointArray): TPointArray;
var
  dot: TPoint;
begin
  for dot in dots do
    if Self.IsValid(dot, True) then
      Result += dot;
end;


(*
## TRSDotFilterArray.Draw
```pascal
procedure TRSDotFilterArray.Draw(out bitmap: TMufasaBitmap);
```
Internal method for drawing the TRSDotFilterArray on a bitmap for debugging.
You can see this in action by calling `Debug(TRSDotFilterArray)`
*)
procedure TRSDotFilterArray.Draw(bitmap: TMufasaBitmap);
var
  i: Int32;
begin
  for i := 0 to High(Self) do
    Self[i].Draw(bitmap);
end;


(*
## TRSMinimap.GetFilteredDot
```pascal
function TRSMinimap.GetFilteredDotArray(dot: ERSMinimapDot; dotFilters: TRSDotFilterArray): TPointArray;
function TRSMinimap.GetFilteredDot(dot: ERSMinimapDot; dotFilters: TRSDotFilterArray): TPoint;
```
Retrieves and filters minimap dots according to the specified TRSDotFilters.
*)
function TRSMinimap.GetFilteredDotArray(dot: ERSMinimapDot; dotFilters: TRSDotFilterArray): TPointArray;
begin
  Result := dotFilters.FilterDots(Self.GetDots(dot));
end;

function TRSMinimap.GetFilteredDot(dot: ERSMinimapDot; dotFilters: TRSDotFilterArray): TPoint;
var
  dots: TPointArray;
begin
  dots := Self.GetFilteredDotArray(dot, dotFilters);
  if dots <> [] then
    Result := dots[0];
end;


(*
## Debug
```pascal
procedure Debug(filter: TRSDotFilter); overload;
procedure Debug(filterArray: TRSDotFilterArray); overload;
```
Debugs a TRSDotFilter or TRSDotFilterArray on the client.
*)
procedure Debug(filter: TRSDotFilter); overload;
var
  bmp: TMufasaBitmap;
begin
  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  if RSClient.DebugOnClient then
  begin
    RSClient.Image().Clear();
    filter.Draw(RSClient.Image());
    Exit;
  end;
  {$ENDIF}
  bmp.FromClient();
  filter.Draw(bmp);
  bmp.Debug();
  bmp.Free();
end;

procedure Debug(filters: TRSDotFilterArray); overload;
var
  bmp: TMufasaBitmap;
begin
  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  if RSClient.DebugOnClient then
  begin
    RSClient.Image().Clear();
    filters.Draw(RSClient.Image());
    Exit;
  end;
  {$ENDIF}
  bmp.FromClient();
  filters.Draw(bmp);
  bmp.Debug();
  bmp.Free();
end;
