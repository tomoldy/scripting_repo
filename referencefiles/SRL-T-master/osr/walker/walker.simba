(*
# Walker

Walker, also commonly refered to as RSWalker and RSW is the go to tool for walking and accurate positioning.

## How does it work?
The details are quite complex but to put it simply, the way it works is:
- A large image of the game map is loaded. This image has to include the region you are on or things won't work as expected.
- A screenshot of your minimap is taken and, rotated north and cleaned (player, npc and item dots are removed).
- This minimap screenshot is then searched on our loaded map image, the closest match is then returned as our position.
- Once we have an accurate position, we can do all sort of things:
  - Accurately get the position of a tile
  - Accurately hover any visible tile with the help of mm2ms
  - Color search a tile

Things to keep in mind with TRSWalker:
- Coordinates are relative to the map loaded. They are literally your pixel position in the map image.
  If you load map A and B and map B contains map A somewhere that is not it's top-left corner,
  their coordinates will not match even if you are standing in the same tile.
  So if you get your position and it tells you are at X = 300 and Y = 500 it means you are in the pixel X = 300 and Y = 500 of your map image.
- Because we search our minimap inside our map to find our position, smaller maps are much faster.
  It's not exactly porportional but almost, so if you have a map that has a total of 1000 pixels and it takes 50ms to get your position,
  a map with 2000 pixels will take almost exactly 100ms.
- A map image cannot be smaller than the minimap and should not even be anywhere close to it's size.
  Any area you think you might be standing on on your script should have at the very minimum a 75 extra pixels of "padding"
  because that's the maximum visible distance of the minimap and even then, slightly more is recommended.

Variables for users to tweak:
- ScreenWalk: When set to true, walker will use only the mainscreen to walk instead of the minimap.
- AdaptiveWalk: When set to true, will enable and disable ScreenWalk depending on your distance to the target,
                making walking use a mix of minimap and mainscreen walking. The closer your are to the target
                the more likely it is to use the mainscreen and the further you are the less likely.
                You can also play with RSW_ADAPTIVE_SCREEN_TOGGLE_DISTANCES values to customize the behavior.
*)

{$DEFINE SRL_WALKER_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

{$H-}
{$I world.graph}
{$H+}

// not a const, allow people to edit.
var
  RSW_ADAPTIVE_SCREEN_TOGGLE_DISTANCES: TPoint = [25,110];

type
(*
## PRSWalker
TRSWalker pointer.
*)
  PRSWalker = ^TRSWalker;

(*
## TRSWalker_OnWalkEvent
Callback object method to use while walking. This can be used to perform custom tasks while walking.

Example:
```pascal
procedure TRSWalker.WalkerTasks(Walker: PRSWalker; Position: TPoint; Destination: TPoint);
begin
  Antiban.RandomTab();
end;

var
  rsw: TRSWalker;
begin
  rsw.Setup('world');
  rsw.OnWalkingEvent := @rsw.WalkerTasks;
end;
```
*)
  TRSWalker_OnWalkEvent = procedure(walker: PRSWalker; position: TPoint; destination: TPoint) of object;

(*
## TRSWalker
TRSWalker is the record responsbile for walking and position.
*)
  TRSWalker = record(TSRLBaseRecord)
    Region: TBox;
    Regions: TBoxArray;
    RegionIndex: Integer;

    Map: TRSWalkerMap;
    Cache: TMatchTemplateRGBCache;
    Scaling: Int32;
    Similarity: Single;

    WebGraph: TWebGraph;

    EnableRunAtEnergy: Int32;

    ScreenWalk, AdaptiveWalk, NearZoomMode, FancyMouse: Boolean;

    ClickRandomness: Int32;
    AdaptiveToggleDistances: TPoint;

    Path: TPointArray;
    PathIndex: Int32;

    MouseAheadTimer: Int32;

    Enabled: Boolean;
    WalkUpText: TStringArray;

    OnWalkEvent: TRSWalker_OnWalkEvent;
    OnWalkEventEx: TRSWalker_OnWalkEvent; //DON'T USE. Will be removed. Use OnWalkEvent.
    OnWalkingEvent: TRSWalker_OnWalkEvent;
  end;

(*
## Walker.InternalSetup
```pascal
procedure TRSWalker.InternalSetup();
```
Internal walker method automatically called by Walker.Setup().
You probably will never need to call this directly.
*)
procedure TRSWalker.InternalSetup();
var
  minimapImage, minimapSample: TMufasaBitmap;
begin
  Self.Name := 'Walker';

  minimapImage := Minimap.GetCleanMinimap();
  minimapSample := Minimap.ScaleMinimap(minimapImage, Self.Scaling);

  Self.Cache := TMatchTemplateRGBCache.Create(Self.Map.ScaledMap.ToMatrix(), MinimapSample.ToMatrix());
  Self.WalkUpText := ['Walk here', 'lk he'];

  minimapSample.Free();
  minimapImage.Free();
end;

(*
## Walker.Free
```pascal
procedure TRSWalker.Free;
```
Internal walker method automatically called on script termination.
You probably will never need to call this directly. If you do, you won't be able to use your walker until you set it up again.
*)
procedure TRSWalker.Free();
begin
  if Assigned(Self.Map) then
    Self.Map.Free();
end;

(*
## Walker.Setup
```pascal
procedure TRSWalker.Setup(map: String; Scaling: Int32 = 8);
procedure TRSWalker.Setup(scaling: Int32 = 8); overload;
procedure TRSWalker.SetupRegions(map: String; aRegions: TBoxArray; scaling: Int32 = 8);
procedure TRSWalker.SetupRegions(aRegions: TBoxArray; scaling: Int32 = 8); overload;
procedure TRSWalker.SetupRegion(map: String; aRegion: TBox; scaling: Int32 = 8);
procedure TRSWalker.SetupRegion(aRegion: TBox; scaling: Int32 = 8); overload;
procedure TRSWalker.SetupFromURL(url: String; map: String = ''; scaling: Int32 = 8; force: Boolean = False); overload;
```
Setup method to be called in scripts.
There's several versions of the method you can call depending on the features you want to use.
All of them have a scaling parameter in common that defaults to 8.
This is used to adjust the downscaling of the map used, lower downscaling makes walker more accurate at the cost of speed.
The speed difference is more or less porportional to the scaling, higher being faster. Unless you have issues with wrong positions you should probably not touch it.

Map is the map name you want to load. The file is assumed to be in the walker maps directory: Simba/Includes/SRL/osr/walker/maps
If the map is not there, an error will be thrown.
You can optionally load only certain regions of the map. This is useful if there's a map that has the area you want but it's too slow due to it's size.
The regions are specified as a TBoxArray and they are cropped from the map you specify on setup and joined into a single map image. This type of setup retains the coordinates of the original map image.
You can also optionally omit the Map name to make walker use the default SRL map which you can find in: Simba/Includes/SRL/osr/walker/map.png

Example:
```pascal
//The following are several setup examples. Keep in mind that if the file doesn't exist in the maps folder, Walker will throw an error.
var
  rsw: TRSWalker;

begin
  rsw.Setup('world');
  rsw.Setup('world.png');
  rsw.Setup('world', 6);
  rsw.Setup();
  rsw.SetupRegions([[4460, 1900, 4800, 2250], [75, 75, 1000, 1000]]); //loads GE and lunar isle.
end;
```
*)
procedure TRSWalker.Setup(map: String; scaling: Int32 = 8);
begin
  Self.Scaling := scaling;
  Self.Map.Load(map, scaling);
  Self.Regions := Self.Map.Regions;
  Self.InternalSetup();
end;

procedure TRSWalker.Setup(scaling: Int32 = 8); overload;
begin
  Self.Setup(WALKER_MAP, scaling);
end;

procedure TRSWalker.SetupRegions(map: String; aRegions: TBoxArray; scaling: Int32 = 8);
begin
  Self.Scaling := scaling;
  Self.Map.Load(map, aRegions, scaling);
  Self.Regions := aRegions;
  Self.InternalSetup();
end;

procedure TRSWalker.SetupRegions(aRegions: TBoxArray; scaling: Int32 = 8); overload;
begin
  Self.SetupRegions(WALKER_MAP, aRegions, scaling);
end;

procedure TRSWalker.SetupRegion(map: String; aRegion: TBox; scaling: Int32 = 8);
begin
  Self.SetupRegions(map, [aRegion], scaling);
end;

procedure TRSWalker.SetupRegion(aRegion: TBox; scaling: Int32 = 8); overload;
begin
  Self.SetupRegion(WALKER_MAP, aRegion, scaling);
end;

procedure TRSWalker.SetupFromURL(url: String; map: String = ''; scaling: Int32 = 8; force: Boolean = False); overload;
var
  client: Int32;
  fileName, path: String;
  tmp: TStringArray;
begin
  if map = '' then
  begin
    tmp := url.Explode('/');
    map := tmp[High(tmp)];
  end;

  fileName := WALKER_MAPS + map;

  if FileExists(fileName) then
  begin
    if not force then
    begin
      Self.Setup(map, scaling);
      Exit;
    end;

    if not DeleteFile(fileName) then
      RaiseException('Failed to delete: ' + fileName);
  end;

  WriteLn 'Downloading file: ' + url + '  >  ' + fileName;
  path := ExtractFileDir(fileName);
  if not ForceDirectories(path) then
    TerminateScript('Failed to create path: ' + path);

  try
    client := InitializeHTTPClient(False);
    GetHTTPPageEx(client, url, fileName);
  finally
    FreeHTTPClient(client);
  end;

  Self.Setup(map, scaling);
end;


(*
## Walker.AddRegion
```pascal
procedure TRSWalker.AddRegion(map: String; start: TPoint);
procedure TRSWalker.AddRegions(map: String; aRegions: TBoxArray);
procedure TRSWalker.AddRegion(map: String; aRegion: TBox); overload;
procedure TRSWalker.AddRegions(aRegions: TBoxArray); overload;
procedure TRSWalker.AddRegion(aRegion: TBox); overload;
procedure TRSWalker.AddRegionFromURL(url: String; start: TPoint; map: String = ''; force: Boolean = False); overload;
```
Add a region to walker on the fly after it is already setup.
*)
procedure TRSWalker.AddRegion(map: String; start: TPoint);
var
  aRegion: TBox;
begin
  if Self = [] then
    Self.Fatal('Walker has to be setup already to add a single region');
  Self.Map.Load(map, start, Self.Scaling, aRegion);
  Self.Regions += aRegion;
  Self.InternalSetup();
end;

procedure TRSWalker.AddRegions(map: String; aRegions: TBoxArray);
var
  b: TBox;
begin
  if Self = [] then
    Self.Fatal('Walker has to be setup already to add a single region');
  for b in aRegions do
  begin
    Self.Map.Load(map, b, Self.Scaling);
    Self.Regions += b;
  end;
  Self.InternalSetup();
end;

procedure TRSWalker.AddRegion(map: String; aRegion: TBox); overload;
begin
  Self.AddRegions(map, [aRegion]);
end;

procedure TRSWalker.AddRegions(aRegions: TBoxArray); overload;
begin
  Self.AddRegions(WALKER_MAP, aRegions);
end;

procedure TRSWalker.AddRegion(aRegion: TBox); overload;
begin
  Self.AddRegions([aRegion]);
end;

procedure TRSWalker.AddRegionFromURL(url: String; start: TPoint; map: String = ''; force: Boolean = False); overload;
var
  client: Int32;
  fileName, path: String;
  tmp: TStringArray;
begin
  if map = '' then
  begin
    tmp := url.Explode('/');
    map := tmp[High(tmp)];
  end;

  fileName := WALKER_MAPS + map;

  if FileExists(fileName) then
  begin
    if not force then
      Exit;

    if not DeleteFile(fileName) then
      RaiseException('Failed to delete: ' + fileName);
  end;

  WriteLn 'Downloading file: ' + url + ' > ' + fileName;
  path := ExtractFileDir(fileName);
  if not ForceDirectories(path) then
    TerminateScript('Failed to create path: ' + path);

  client := InitializeHTTPClient(False);
  try
    GetHTTPPageEx(client, url, fileName);
  finally
    FreeHTTPClient(client);
  end;

  Self.AddRegion(map, start);
end;


(*
## Walker.ScaledSearch
```pascal
function TRSWalker.ScaledSearch(Bitmap: TMufasaBitmap; Samples: Int32): TPointArray;
```
Internal walker method used to get an initial TPointArray of possible positions.
This is performed in a scaled down map with a scaled down minimap. This is very innacurate by itself but by ruling down most of the map
in a scaled search before doing a full sized search speed has a dramatic boost.
You probably won't ever need to call this directly.
*)
function TRSWalker.ScaledSearch(Bitmap: TMufasaBitmap; Samples: Int32): TPointArray;
var
  Sample: TMufasaBitmap;
  Matrix: TSingleMatrix;
begin
  Sample := Minimap.ScaleMinimap(Bitmap, Self.Scaling);
  Matrix := MatchTemplateMask(Self.Cache, Sample.ToMatrix, TM_CCOEFF_NORMED);

  while Length(Result) < Samples do
    with Matrix.ArgMax() do
    begin
      Result += Offset(Sample.GetWidth() div 2, Sample.GetHeight() div 2) * Self.Scaling;
      Matrix.Fill([X - 5, Y - 5, X + 5, Y + 5], 0);
    end;

  Result := Result.Offset(Self.Scaling div 2, Self.Scaling div 2);

  Sample.Free();
end;

(*
## Walker.FullSearch
```pascal
function TRSWalker.FullSearch(Templ, World: TMufasaBitmap; Position: TPoint; out Match: Single): TPoint;
```
Internal walker method used to get the player position.
This is used by TRSWalker.GetMyPos() to determine how likely is the **Position** passed in, our actual position.
This likelyhood is returned with **Match** which ranges from 0 to 1.
You probably won't ever need to call this directly.
*)
function TRSWalker.FullSearch(Templ, World: TMufasaBitmap; Position: TPoint; out Match: Single): TPoint;
const
  TEMPL_SIZE = 25;
  SLICE_SIZE = TEMPL_SIZE + 45;
  SCALING = 2;
var
  Slice, SliceScaled, TemplScaled: TMufasaBitmap;
  Matrix: TSingleMatrix;
  Area: TBox;
begin
  Area := [
    Max(0, Position.X - SLICE_SIZE),
    Max(0, Position.Y - SLICE_SIZE),
    Min(World.GetWidth()  - 1, Position.X + SLICE_SIZE),
    Min(World.GetHeight() - 1, Position.Y + SLICE_SIZE)
  ];

  Slice := World.Copy(Area.X1, Area.Y1, Area.X2, Area.Y2);
  SliceScaled := Slice.Downsample(SCALING);

  TemplScaled := Templ.Downsample(SCALING);
  with TemplScaled.GetCenter() do
    TemplScaled.Crop(X - TEMPL_SIZE, Y - TEMPL_SIZE, X + TEMPL_SIZE, Y + TEMPL_SIZE);

  Matrix := SliceScaled.MatchTemplate(TemplScaled, TM_CCOEFF_NORMED);
  with Matrix.ArgMax() do
  begin
    Match := Matrix[Y, X];

    Result.X := Area.X1 + (X + TEMPL_SIZE) * SCALING;
    Result.Y := Area.Y1 + (Y + TEMPL_SIZE) * SCALING;
  end;

  TemplScaled.Free();
  SliceScaled.Free();
  Slice.Free();
end;


(*
## Walker.GetMyPos
```pascal
function TRSWalker.GetMyPos(): TPoint;
```
Returns the players current position on the loaded map.

Example::
```pascal
WriteLn(Walker.GetMyPos());

// Check to see the match percentage if needed
WriteLn(Walker.Similarity);
```
*)
function TRSWalker.GetMyPos(): TPoint;
const
  SAMPLES = 3;
var
  Bitmap: TMufasaBitmap;
  TPA: TPointArray;
  I: Int32;
  Hit: TPoint;
  Match: Single;
begin
  Self.RegionIndex := -1;
  Self.Region := [];
  Self.Similarity := 0;

  Bitmap := Minimap.GetCleanMinimap();

  TPA := Self.ScaledSearch(Bitmap, SAMPLES);
  for I := 0 to High(TPA) do
  begin
    Hit := Self.FullSearch(Bitmap, Self.Map.Map, TPA[I], Match);

    if Match > Self.Similarity then
    begin
      Self.Similarity := Match;

      Result := Hit;
    end;
  end;

  Self.RegionIndex := Self.Map.RegionIndexFromPoint(Result);
  if Self.RegionIndex > -1 then
  begin
    Self.Region := Self.Map.Regions[Self.RegionIndex];

    Result := Self.Map.RegionToGlobal(Result);
  end;

  Bitmap.Free();
end;

(*
## Walker.WorldToMM
```pascal
function TRSWalker.WorldToMM(playerPoint, walkerPoint: TPoint; Radians: Double): TPoint;
function TRSWalker.WorldToMM(walkerPoint: TPoint): TPoint; overload;
```
Converts a walker coordinate to a point in the minimap.

Example:
```pascal
var
  rsw: TRSWalker;
  p: TPoint;
  bitmap: TMufasaBitmap;

begin
  rsw.Setup([RSWalkerRegions.GRAND_EXCHANGE]); //Make sure you are in GE for this example.
  p := rsw.WorldToMM([4620, 2100]);            //This is just a random point in the ge with SRL map.

  bitmap.FromClient();
  bitmap.DrawCross(p, 4, $FFFFFF);
  bitmap.Free();
end;
```
*)
function TRSWalker.WorldToMM(playerPoint, walkerPoint: TPoint; Radians: Double): TPoint;
begin
  Result := walkerPoint - playerPoint + Minimap.Center;
  Result := Result.Rotate(Radians, Minimap.Center);
end;

function TRSWalker.WorldToMM(walkerPoint: TPoint): TPoint; overload;
begin
  Result := Self.WorldToMM(Self.GetMyPos(), walkerPoint, Minimap.GetCompassAngle(False));
end;

(*
## Walker.CheckRunEnergy
```pascal
procedure TRSWalker.CheckRunEnergy();
```
Internal method used to check and enable the player run.
You will probably never need to call this directly.

The values used are hardcoded and if you don't like them, it's recommended you override the method.
The following example shows how one could override the function to enable run at 50% energy everytime, keep in mind though, you shouldn't do this, you should add randomness to it!
```
*)
procedure TRSWalker.CheckRunEnergy();
begin
  if Minimap.IsRunEnabled() or (Minimap.GetLevel(ERSMinimapOrb.ENERGY) < Self.EnableRunAtEnergy) then
    Exit;

  Self.EnableRunAtEnergy := SRL.SkewedRand(40, 10, 100);
  Minimap.EnableRun();
end;

(*
## Walker.AdaptiveWalkCheck
```pascal
procedure TRSWalker.AdaptiveWalkCheck(Position: TPoint);
```
Internal method used to check if adaptive walk should toggle and toggle TRSWalker.ScreenWalk.
You will probably never need to call this directly.
*)
procedure TRSWalker.AdaptiveWalkCheck(Position: TPoint);
var
  wasEnabled: Boolean;
begin
  wasEnabled := Self.ScreenWalk;
  Self.ScreenWalk := False;
  if ((Position.DistanceTo(Self.Path[0]) < Self.AdaptiveToggleDistances.X) or
      (Position.DistanceTo(Self.Path[High(Self.Path)]) < Self.AdaptiveToggleDistances.Y)) and
     (Random() > 0.05) then
    Self.ScreenWalk := True

  else if Random() < 0.02 then
    Self.ScreenWalk := True;

  if (not wasEnabled) and (Self.ScreenWalk) and (Self.FancyMouse) and (Random() < 0.75) and (Minimap.IsPointOn(Mouse.Position)) then
    Self.DoMouseAhead(Position, True);
end;

(*
## Walker.DoMouseAhead
```pascal
procedure TRSWalker.DoMouseAhead(Position: TPoint; Forced: Boolean = False);
```
Internal method used to pre-hover the next walking step.
You will probably never need to call this directly.
*)
procedure TRSWalker.DoMouseAhead(Position: TPoint; Forced: Boolean = False);
var
  flag: TPoint;
  dist: Int32;

  procedure MouseToNextPoint(myPos: TPoint);
  var
    mmPos: TPoint;
    idx: Int32;
    angle: Double;
    SlowMouse: TMouse;
  begin
    angle := Minimap.GetCompassAngle(False);
    idx := Self.PathIndex;
    while (idx < High(Self.Path)) and Self.IsWalkable(Self.Path[idx+1], myPos, angle) do
      Inc(idx);

    if (Idx = High(Self.Path)) and (Random(10) > 2) then Exit();

    mmPos := Self.Path[idx] - myPos + minimap.Center;
    mmPos := mmPos.Rotate(angle, Minimap.Center);

    if Self.ScreenWalk then
      mmPos := Minimap.PointToMSRect(mmPos).Mean();

    SlowMouse := Mouse;
    SlowMouse.Speed := Random(6,10);
    if not Self.ScreenWalk then
      SlowMouse.Move(Box(mmPos, Random(5,16), Random(5,16)))
    else
      SlowMouse.Move(Box(mmPos, Random(15,36), Random(15,36)));
  end;
begin
  if (not Forced) then
    if (GetTimeRunning() < Self.MouseAheadTimer) or (not Minimap.FindFlag(Flag)) then
      Exit();

  dist := Trunc(flag.DistanceTo(Minimap.Center));
  if InRange(dist, Random(5,10), Random(60,70)) then
    MouseToNextPoint(Position);

  Self.MouseAheadTimer := GetTimeRunning() + SRL.SkewedRand(0,50,500);
  if Self.ScreenWalk then
    Self.MouseAheadTimer += SRL.SkewedRand(0,50,500);
end;

(*
## Walker.WaitMoving
```pascal
procedure TRSWalker.WaitMoving(Destination: TPoint; WaitUntilDistance: Int32);
```
Internal method used to wait while we are moving using walker.
You will probably never need to call this directly.

This is where TRSWalker.OnWalkingEvent are called.
*)
procedure TRSWalker.WaitMoving(Destination: TPoint; WaitUntilDistance: Int32);
var
  Timeout, Countdown: TCountDown;
  Position: record
    Current, Previous: TPoint;
  end;
  awct: Int64;
begin
  Timeout.Init(Round(SRL.TruncatedGauss(20000, 25000)));

  Countdown.Init(Round(SRL.TruncatedGauss(500, 1500)));

  // initial timeout needs to be longer, as we might not have started walking yet.
  Countdown.Timeout := GetTickCount() + Round(SRL.TruncatedGauss(2500, 3500));

  Position.Previous := Self.GetMyPos();
  Position.Current := Position.Previous;

  // Break from the loop if:
  // - Haven't moved in 500ms
  // - Been walking for over 20 seconds
  // - Within `WaitUntilDistance` to the destination
  repeat
    Self.CheckRunEnergy();

    Position.Previous := Position.Current;
    Position.Current := Self.GetMyPos();
    if Position.Current <> Position.Previous then
      Countdown.Restart();

    if (@Self.OnWalkingEvent <> nil) then
      Self.OnWalkingEvent(@Self, Position.Current, Destination);

    if (Self.AdaptiveWalk) and (GetTickCount() > awct) then
    begin
      Self.AdaptiveWalkCheck(Position.Current);
      awct := GetTickCount() + 1000;
    end;

    if Self.FancyMouse then
      Self.DoMouseAhead(Position.Current);

    if not Self.Enabled then
      Break;

    Wait(100);
  until Timeout.IsFinished() or Countdown.IsFinished() or (Position.Current.DistanceTo(Destination) <= WaitUntilDistance);
end;

(*
## Walker.Click
```pascal
function TRSWalker.Click(minimapPoint: TPoint; Randomness: Int32): Boolean;
```
Internal method used by walker to handle clicking while walking.
You will probably never need to call this directly.

If you wish to modify certain walker behaviors, it can be a good approach to override this function.
*)
function TRSWalker.Click(minimapPoint: TPoint; Randomness: Int32): Boolean;
var
  p: TPoint;
  i: Int32;
  upText: String;
begin
  Result := True;
  Self.CheckRunEnergy();

  if Self.ScreenWalk then
  begin
    p := Minimap.RandomPointOnZoomRectangle(minimapPoint, randomness);
    Mouse.Move(Minimap.PointToMSRect(p).Mean());
    upText := MainScreen.GetUpText();

    if not upText.ContainsAny(Self.WalkUpText) then
      Exit(ChooseOption.Select('Walk here'));
  end
  else
    Mouse.Move(Minimap.RandomPointOn(minimapPoint, randomness));

  Mouse.Click(MOUSE_LEFT);

  // 15% chance of spam clicking
  if (Randomness > 0) and (Random() < 0.15) then
    for i := 0 to Random(3) do
    begin
      Mouse.Click(Mouse.Position(), MOUSE_LEFT);
      Wait(0, 150, wdLeft);
    end;
end;

(*
## Walker.WalkStepHelper()
```pascal
function TRSWalker.WalkStepHelper(playerPoint, walkerPoint: TPoint; out minimapPoint: TPoint): Boolean;
```
Internal method used by walker to help walking steps.
You will probably never need to call this directly.
*)
function TRSWalker.WalkStepHelper(playerPoint, walkerPoint: TPoint; out minimapPoint: TPoint): Boolean;
var
  event_time: Int64;
begin
  event_time := GetTickCount();

  // run events
  if (@Self.OnWalkEvent <> nil) then
    Self.OnWalkEvent(@Self, playerPoint, walkerPoint);

  if (@Self.OnWalkEventEx <> nil) then
  begin
    Self.DebugLn('TRSWalker.OnWalkEventEx is deprecated and will be removed. Please use TRSWalker.OnWalkEvent instead!');
    Self.OnWalkEventEx(@Self, playerPoint, walkerPoint);
  end;

  // in case the events used a bit of time, while our character was moving towards a point, we have to update the current position.
  if GetTickCount() - event_time > 100 then
    playerPoint := Self.GetMyPos();

  if not Self.Enabled then
    Exit;

  minimapPoint := Self.WorldToMM(playerPoint, walkerPoint, Minimap.GetCompassAngle(False));

  if Self.ScreenWalk then
    Result := Minimap.PointOnZoomRectangle(minimapPoint)
  else
    Result := Minimap.IsPointOn(minimapPoint);
end;

(*
## Walker.WalkFinalStep
```pascal
function TRSWalker.WalkFinalStep(playerPoint, walkerPoint: TPoint; WaitUntilDistance: Int32): Boolean;
```
Internal method used by walker when finishing walking a path.
You will probably never need to call this directly but it can be used to take a single step.
*)
function TRSWalker.WalkFinalStep(playerPoint, walkerPoint: TPoint; waitUntilDistance: Int32): Boolean;
var
  minimapPoint: TPoint;
begin
  if not Self.WalkStepHelper(playerPoint, walkerPoint, minimapPoint) then
    Exit(not Self.Enabled);

  if (Minimap.Center.DistanceTo(minimapPoint) < 5) then
    Exit(True);

  Result := Self.Click(minimapPoint, 0);
  if Result then
    Self.WaitMoving(walkerPoint, waitUntilDistance);
end;

(*
## Walker.WalkStep
```pascal
function TRSWalker.WalkStep(playerPoint, walkerPoint: TPoint): Boolean;
```
Internal method used by walker while walking a path.
You will probably never need to call this directly.
*)
function TRSWalker.WalkStep(playerPoint, walkerPoint: TPoint): Boolean;
var
  minimapPoint: TPoint;
  waitUntilDistance: Int32;
begin
  if not Self.WalkStepHelper(playerPoint, walkerPoint, minimapPoint) then
    Exit(not Self.Enabled);

  if (Minimap.Center.DistanceTo(minimapPoint) < 5) then
    Exit(True);

  Result := Self.Click(minimapPoint, Self.ClickRandomness);

  if Result then
  begin
    if (Random() < 0.15) then // 15% chance of randomness else mean
      waitUntilDistance := Random(Distance(Minimap.Center, minimapPoint))
    else
      waitUntilDistance := SRL.NormalRange(0, Distance(Minimap.Center, minimapPoint) + 10);

    Self.WaitMoving(walkerPoint, waitUntilDistance);
  end;
end;

(*
## Walker.IsWalkable
```pascal
function TRSWalker.IsWalkable(walkerPoint: TPoint; playerPoint: TPoint; Angle: Double): Boolean;
```
Internal method used by walker to decide if the destination point is within 1 click reach.
You will probably never need to call this directly.
*)
function TRSWalker.IsWalkable(walkerPoint: TPoint; playerPoint: TPoint; angle: Double): Boolean;
var
  mmPoint: TPoint;
  b: TBox;
begin
  mmPoint := Self.WorldToMM(playerPoint, walkerPoint, angle);
  if not Self.ScreenWalk then
    Exit(Minimap.IsPointOn(mmPoint, -1));

  b := Minimap.PointToMSRect(mmPoint, 1, 1, Angle).Bounds();
  if MainScreen.IsVisible(b.Middle) then
    Result := CountColor($0, b.Expand(4, MainScreen.Bounds)) = 0; //Client doesn't always render everything when zoomed out
end;


(*
## TRSWalker.WalkPath
```pascal
function TRSWalker.WalkPath(Path: TPointArray; WaitUntilDistance: Int32 = 0): Boolean;
```
Walks a path of points taken from the loaded map. We advice that WaitUntilDistance is not 0.

Parameters:
- Path
	Array of points taken from the loaded map to walk. Must be ordered from start to finish.
- WaitUntilDistance
	Determines when the method returns once the final point has been clicked. Default value: 0.
	| *WaitUntilDistance=0* waits until the player has reached the final point.
	| *WaitUntilDistance=20* waits until the player is within 20 pixels of the final point.

Example:
```pascal
Walker.WalkPath([[100,100],[120,120],[140,140],[160,160],[180,180]]);
```
*)
function TRSWalker.WalkPath(path: TPointArray; WaitUntilDistance: Int32 = 0): Boolean;

  function BuildPath(TPA: TPointArray): TPointArray;
  var
    I, J: Int32;
    Line: TPointArray;
  begin
    for I := 1 to High(TPA) do
    begin
      Line := TPAFromLine(TPA[I-1], TPA[I]);

      J := 0;
      while J < High(Line) do
      begin
        Result += Line[J];

        // keep the steps low, as mainscreen walking might just have a few tiles visible when NearZoomMode is set True
        if (Self.NearZoomMode) and ((Self.ScreenWalk) or (Self.AdaptiveWalk)) then
          Inc(J, Random(1, 4))
        else
          Inc(J, Random(3, 12));
      end;
    end;

    Result += TPA[High(TPA)];
  end;

  function AdvancePath(var Index: Int32; playerPoint: TPoint): Boolean;
  var
    Angle: Double := Minimap.GetCompassAngle(False);
    Previous: Int32 := Index;
    Nearest, Furthest: Int32;
  begin
    Nearest := Self.Path.Find(Self.Path.Sorted(playerPoint)[0]);
    Self.PathIndex := Nearest;

    while (Nearest < High(Self.Path)) and (not Self.IsWalkable(Self.Path[Nearest+1], playerPoint, Angle)) do
      Inc(Nearest);

    Furthest := Nearest;
    while (Furthest < High(Self.Path)) and Self.IsWalkable(Self.Path[Furthest+1], playerPoint, Angle) do
      Inc(Furthest);

    Index := Furthest;

    // do the result check before randomness to avoid false error
    Result := (Index > Previous);

    // 15% chance of randomness else furthest
    if Random() < 0.15 then
      Index := Random(Nearest, Furthest);
  end;

var
  playerPoint: TPoint;
  Index, Fails: Int32;
begin
  Self.Enabled := True;
  playerPoint := Self.GetMyPos();
  Self.Path := BuildPath([playerPoint] + path);

  if Self.AdaptiveWalk then
  begin
    Self.AdaptiveToggleDistances.X := RSW_ADAPTIVE_SCREEN_TOGGLE_DISTANCES.X + Random(-6,6);
    Self.AdaptiveToggleDistances.Y := RSW_ADAPTIVE_SCREEN_TOGGLE_DISTANCES.Y + Random(-16,16);
    Self.AdaptiveWalkCheck(playerPoint);
  end;

  while Self.Enabled and (Fails < 10) do
  begin
    playerPoint := Self.GetMyPos();

    if AdvancePath(Index, playerPoint) then
      Fails := 0
    else
    begin
      // Likely haven't moved far enough to advance to next point
      if Inc(Fails) < 5 then
      begin
        Wait(300);
        Continue;
      end;

      // screen walking struggles, so disable it
      if Self.AdaptiveWalk then
        Self.ScreenWalk := False
      else if (Fails = 5) then
        case SRL.Dice(50) of
            True:  Minimap.SetCompassAngle(Minimap.GetCompassAngle() - SRL.TruncatedGauss(30, 360, 3));
            False: Minimap.SetCompassAngle(Minimap.GetCompassAngle() + SRL.TruncatedGauss(30, 360, 3));
          end;

      if (Fails mod 2 = 0) then
        Minimap.WaitFlag();
    end;

    if Index = High(Self.Path) then
    begin
      if Self.WalkFinalStep(playerPoint, Self.Path[Index], WaitUntilDistance) then
        Exit(True);
    end;

    Self.WalkStep(playerPoint, Self.Path[Index]);
  end;

  if Fails = 10 then
    Self.DebugLn('Failed to advance path');
end;

(*
## TRSWalker.WalkBlind
```pascal
function TRSWalker.WalkBlind(Destination: TPoint; WaitUntilDistance: Int32 = 0): Boolean;
```
"Blindly" walks to a point taken from the loaded map.
A straight line is generated between the player's position and destination which is then walked.

Parameters:
- Destination
  Destination point taken from the loaded map.
- WaitUntilDistance
	Determines when the method returns once the final point has been clicked. Default value: 0.
	| *WaitUntilDistance=0* waits until the player has reached the final point.
	| *WaitUntilDistance=20* waits until the player is within 20 pixels of the final point.

Example:
```pascal
Walker.WalkBlind([300, 300]);
```
*)
function TRSWalker.WalkBlind(Destination: TPoint; WaitUntilDistance: Int32 = 0): Boolean;
begin
  Result := Self.WalkPath([Destination], WaitUntilDistance);
end;

(*
## TRSWalker.WebWalk
```pascal
function TRSWalker.WebWalk(Destination: TPoint; WaitUntilDistance: Int32 = 0; PathRandomness: Double = 0): Boolean;
```
Web walks to the destination point on the loaded map. Does **not** handle any obstacles.
Please run ``webber.simba`` to see how webgraphs are built.

Pre built webgraphs are available for "World" and "Zeah" when used.

Parameters:
- Destination
	Destination point taken from the loaded map.
- WaitUntilDistance
	Determines when the method returns once the final point has been clicked. Default value: 0.
	| *WaitUntilDistance=0* waits until the player has reached the final point.
	| *WaitUntilDistance=20* waits until the player is within 20 pixels of the final point.
- PathRandomness
	Randomness to add to the path so the absoulte shortest path isn't always taken. Must be between 0..1

Example:
```pascal
var Walker: TRSWalker;

Walker.Setup('world');
Walker.WebWalk([4595, 3575]); // Lumbridge

// Or use a location from the webgraph
Walker.WebWalk(WorldWeb.LOCATION_LUMBRIDGE);
```
*)
function TRSWalker.WebWalk(Destination: TPoint; WaitUntilDistance: Int32 = 0; PathRandomness: Double = 0): Boolean;
var
  Path: TPointArray;
begin
  if not InRange(PathRandomness, 0, 1) then
    Self.Fatal('TRSWalker.WebWalk `PathRandomness` must be within 0..1');

  Path   := Self.WebGraph.PathBetween(Self.GetMyPos, Destination, PathRandomness);
  Result := Self.WalkPath(Path, WaitUntilDistance);
end;


(*
## TRSWalker.DebugPosition
```pascal
procedure TRSWalker.DebugPosition();
```
Debugs the player position in the currently loaded map.

Example:
```pascal
var
  rsw: TRSWalker;
begin
  rsw.Setup();
  while True do
    rsw.DebugPosition();
end;
```
*)
procedure TRSWalker.DebugPosition();
var
  X1, Y1, X2, Y2, X, Y: Int32;
  P: TPoint;
  Bitmap, WorldBitmap, MinimapBitmap: TMufasaBitmap;
  Time: Double;
  Position: TPoint;
  Lines: TStringArray;
begin
  Freeze();

  Time := PerformanceTimer();
  Position := Self.GetMyPos();
  Time := PerformanceTimer() - Time;

  if Self.RegionIndex > -1 then
    Position := Self.Map.GlobalToRegion(Self.RegionIndex, Position);

  X1 := Max(0, Position.X - 200);
  Y1 := Max(0, Position.Y - 200);
  X2 := Min(X1 + 400, Self.Map.Map.GetWidth()  - 1);
  Y2 := Min(Y1 + 400, Self.Map.Map.GetHeight() - 1);

  X := Position.X - X1;
  Y := Position.Y - Y1;

  WorldBitmap := Self.Map.Map.Copy(X1, Y1, X2, Y2);
  WorldBitmap.LineTo(Point(0, Y), Point(X2 - X1, Y), $00FF00);
  WorldBitmap.LineTo(Point(X, 0), Point(X, Y2 - Y1), $00FF00);
  WorldBitmap.DrawBox([X - 2, Y - 2, X + 2, Y + 2], $FFFFFF);

  MinimapBitmap := Minimap.ScaleMinimap(Minimap.GetCleanMinimap(), 1);

  P := MinimapBitmap.GetCenter();
  if RSClient.Mode = ERSClientMode.FIXED then
    P := P.Offset(0, 8).Rotate(-Minimap.GetCompassAngle(False), P);

  MinimapBitmap.DrawBoxFilled([P.X - 1, P.Y - 1, P.X + 1, P.Y + 1], False, $FFFFFF);

  Lines := [
    'Region: ' + ToStr(Self.RegionIndex),
    'Acc: '    + ToStr(Round(Self.Similarity, 3)),
    'Time: '   + ToStr(Round(Time)) + ' ms'
  ];

  Bitmap.Init();
  Bitmap.SetSize(430 + MinimapBitmap.GetWidth(), 420);
  Bitmap.DrawClear($403027);

  Bitmap.SetFontName('Consolas');
  Bitmap.SetFontSize(18);
  Bitmap.SetFontAntialiasing(True);

  Bitmap.DrawText(Format('(%d, %d)', [Position.X - Self.Region.X1, Position.Y - Self.Region.Y1]), [420, 20 + MinimapBitmap.GetHeight()], $FFFFFF);
  Bitmap.DrawText(Lines[0], [420, 350], $FFFFFF);
  Bitmap.DrawText(Lines[1], [420, 370], $FFFFFF);
  Bitmap.DrawText(Lines[2], [420, 390], $FFFFFF);

  WorldBitmap.DrawTransparent(10, 10, Bitmap);
  WorldBitmap.Free();

  MinimapBitmap.DrawTransparent(420, 10, Bitmap);
  MinimapBitmap.Free();

  Bitmap.Debug();
  DisplayDebugImgWindow(Bitmap.getWidth, bitmap.getHeight());
  Bitmap.Free();

  UnFreeze();
end;

(*
## TRSWalker.GetTileMS
```pascal
function TRSWalker.GetTileMSEx(Me, Loc: TPoint; Height:Double=0; Offx,Offy:Double=0): TRectangle;
function TRSWalker.GetTileMS(Loc: TPoint; Height:Double=0; Offx,Offy:Double=0): TRectangle;
```
Returns a tile on the mainscreen with the help of walker and mm2ms.

Example:
```pascal
Debug(rsw.GetTileMS(rsw.GetMypos() + [10, 10]));
```
*)
function TRSWalker.GetTileMSEx(Me, Loc: TPoint; Height:Double=0; Offx,Offy:Double=0): TRectangle;
var
  angle: Double;
begin
  Loc   := Minimap.Center + (Loc - Me);
  angle := Minimap.GetCompassAngle(False);
  Loc   := Loc.Rotate(angle, Minimap.Center);

  Result := Minimap.VectorToMSRect(Vec3(Loc.x - offx, Loc.y - offy, Height), 1, 1, angle);
end;

function TRSWalker.GetTileMS(Loc: TPoint; Height:Double=0; Offx,Offy:Double=0): TRectangle;
begin
  Result := Self.GetTileMSEx(Self.GetMyPos(), Loc, Height, Offx, Offy);
end;

(*
## TRSWalker.MMToWorld
```pascal
function TRSWalker.MMToWorldEx(me, loc: TPoint; roll: Single = $FFFF): TPoint;
function TRSWalker.MMToWorld(loc: TPoint; roll: Single = $FFFF): TPoint;
```
Converts a point on the minimap to a walker point.
*)
function TRSWalker.MMToWorldEx(me, loc: TPoint; roll: Single = $FFFF): TPoint;
begin
  Result := me + loc - Minimap.Center;
  if (roll = $FFFF) then
    roll := Minimap.GetCompassAngle(False);
  Result := Result.Rotate(-roll, me);
end;

function TRSWalker.MMToWorld(loc: TPoint; roll: Single = $FFFF): TPoint;
begin
  if (roll = $FFFF) then
    roll := Minimap.GetCompassAngle(False);
  Result := Self.MMToWorldEx(Self.GetMyPos(), loc, roll);
end;

(*
## TRSWalker.MSToWorld
```pascal
function TRSWalker.MSToWorldEx(Me, Loc: TPoint; Height: Int32 = 0; Accuracy: Double = 0.2): TPoint;
function TRSWalker.MSToWorld(Loc: TPoint; Height: Int32=0; Accuracy:Double=0.2): TPoint;
```
Converts a point on the mainscreen to a walker point.
*)
function TRSWalker.MSToWorldEx(Me, Loc: TPoint; Height: Int32 = 0; Accuracy: Double = 0.2): TPoint;
begin
  Result := Self.MMToWorldEx(me, MainScreen.PointToMM(Loc, Height, Accuracy).ToPoint(), Minimap.GetCompassAngle(False));
end;

function TRSWalker.MSToWorld(Loc: TPoint; Height: Int32=0; Accuracy:Double=0.2): TPoint;
begin
  Result := Self.MSToWorldEx(Self.GetMyPos(), Loc, Height, Accuracy);
end;
